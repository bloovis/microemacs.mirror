dnl Autoconf configure script for MicroEMACS.
dnl Process this file with autoconf to produce a configure script.

AC_INIT
AC_CONFIG_SRCDIR([display.c])
AC_PROG_CXX
AC_CANONICAL_TARGET
AC_CONFIG_HEADERS([config.h])
AC_CHECK_HEADERS(unistd.h)

CFLAGS_DEBUG="-g -O"
CFLAGS_OPTIMIZE="-O"

EXTRA_OBJS=""
case "${host}" in
*hpux*)		DEFINES="$DEFINES -DHPUX"; ;;
x86_64*linux*)	DEFINES="$DEFINES -DLINUX";
		LDFLAGS="-L/usr/lib64/termcap" ;;
*linux*)	DEFINES="$DEFINES -DLINUX" ;;
*solaris*)	DEFINES="$DEFINES -DSOLARIS"; EXTRA_OBJS=bcopy.o;
                bcopy=bcopy.s ;;
*cygwin)	DEFINES="$DEFINES -DCYGWIN" ;;
*mingw*)	DEFINES="$DEFINES -DMINGW" ;;
*bsd*)		LDFLAGS="-L/usr/local/lib";
		CFLAGS_INCLUDE="-I/usr/local/include" ;;
*)		DEFINES="$DEFINES -DUNKNOWN" ;;
esac
echo host is ${host}

dnl Set system and tty directories based on arguments.
AC_ARG_WITH(linux,
[  --with-linux            Use Linux-specific instead of generic UNIX I/O],
[sysdir=sys/linux],
[sysdir=sys/unix])

AC_ARG_WITH(termcap,
[  --with-termcap          Use termcap instead of ncurses for screen I/O],
[ttydir=tty/termcap
LIBS="-ltermcap"],
[ttydir=tty/ncurses
LIBS="-lncursesw"])

AC_ARG_WITH(ntconsole,
[  --with-ntconsole        Use NT-specific console I/O instead of generic UNIX I/O],
[ttydir=tty/nt])

AC_ARG_WITH(ruby,
[  --with-ruby             Add Ruby scripting support],
[if test $withval = "rpc" ; then
  CFLAGS_RUBY="-DUSE_RUBY=1 -DRUBY_RPC=1"
  EXTRA_OBJS="$EXTRA_OBJS ruby.o rubyrpc.o"
  LIBS="$LIBS -ljson-c"
else
  EXTRA_OBJS="$EXTRA_OBJS ruby.o rubyapi.o"
  if test $withval = "yes" ; then
    RUBY=ruby
  else
    version=$withval
    pc=`find $HOME/.rbenv/versions/$version -name "*.pc" | head -1`
    if test -z "$pc" ; then
      pc=`find /usr/local/lib/pkgconfig -name "ruby-*.pc" | head -1`
    fi   
    if test -f "$pc" ; then
      export PKG_CONFIG_PATH=`dirname $pc`
      RUBY=`basename $pc .pc`
    else
      RUBY=ruby
    fi
  fi
  libdir=`pkg-config --variable=libdir $RUBY`
  libruby_so=`pkg-config --variable=LIBRUBY_SO $RUBY`
  fullruby_so=$libdir/$libruby_so
  if test -f $fullruby_so ; then
    LIBRUBY=$fullruby_so
  else
    LIBRUBY=$libruby_so
  fi
  CFLAGS_RUBY="`pkg-config --cflags $RUBY` -DUSE_RUBY=1 -DLIBRUBY=$LIBRUBY"
  case "${host}" in
  *linux*) LIBS="$LIBS -ldl" ;;
  esac
fi
],
[CFLAGS_RUBY=""
])

AC_ARG_WITH(pcre2,
[  --with-pcre2            Use PCRE2 for regular expressions],
[CFLAGS_PCRE2="-DUSE_PCRE2=1"
LIBS="$LIBS -lpcre2-8"],
[CFLAGS_PCRE2=""
])

dnl Set host-specific system and tty directories
case "${host}" in
*mingw*)	ttydir=tty/mingw;
		sysdir=sys/mingw;
		LIBS="" ;;
esac

dnl we might have different ones depending on target platform.
# Set the default compiler switches based on the --enable-debug option

AC_ARG_ENABLE(debug,
[  --enable-debug          Build with debugging symbols],
[CFLAGS_DEFAULT=CFLAGS_DEBUG],
[CFLAGS_DEFAULT=CFLAGS_OPTIMIZE])

AC_SUBST(sysdir)
AC_SUBST(ttydir)
AC_SUBST(DEFINES)
AC_SUBST(EXTRA_OBJS)
AC_SUBST(LIBS)
AC_SUBST(CFLAGS_DEBUG)
AC_SUBST(CFLAGS_DEFAULT)
AC_SUBST(CFLAGS_INCLUDE)
AC_SUBST(CFLAGS_OPTIMIZE)
AC_SUBST(CFLAGS_RUBY)
AC_SUBST(CFLAGS_PCRE2)

AC_CONFIG_FILES([Makefile])
AC_OUTPUT
